/*
 * Copyright 2015 Open mHealth
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray' // see https://github.com/bintray/gradle-bintray-plugin for details

archivesBaseName = 'omh-schema-sdk'
version = '1.0.6-SNAPSHOT'

ext {
    jacksonVersion = '2.6.3'
}

dependencies {
    compile 'com.google.guava:guava:18.0'
    compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
    compile 'com.google.code.findbugs:jsr305:3.0.0'

    testCompile 'com.github.fge:json-schema-validator:2.2.6'
    testCompile 'org.testng:testng:6.8.21'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

test {
    useTestNG()
    workingDir = '..'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        jar(MavenPublication) {
            artifactId 'omh-schema-sdk'
            from components.java
        }
        sources(MavenPublication) {
            artifactId 'omh-schema-sdk'
            artifact sourcesJar
        }
        javadoc(MavenPublication) {
            artifactId 'omh-schema-sdk'
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_api_key') ? bintray_api_key : ''

    publications = ['jar', 'sources', 'javadoc']
    dryRun = false
    publish = false
    pkg {
        repo = 'maven'
        name = 'omh-schema-sdk'
        userOrg = 'openmhealth'
        desc = 'A library containing Java classes corresponding to Open mHealth schemas.'
        websiteUrl = 'https://github.com/openmhealth/schemas'
        issueTrackerUrl = 'https://github.com/openmhealth/schemas/issues'
        vcsUrl = 'https://github.com/openmhealth/schemas.git'
        licenses = ['Apache-2.0']
        labels = ['schemas']
        publicDownloadNumbers = true
    }
}
