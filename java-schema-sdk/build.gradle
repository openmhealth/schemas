/*
 * Copyright 2016 Open mHealth
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'org.openmhealth.schema'
archivesBaseName = 'omh-schema-sdk'
version = '1.2.2-SNAPSHOT'

ext {
    jacksonVersion = '2.8.9'
    javaVersion = 1.8
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:23.0'
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
    implementation 'com.google.code.findbugs:jsr305:3.0.0'

    testImplementation 'com.github.fge:json-schema-validator:2.2.6'
    testImplementation 'org.testng:testng:6.8.21'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

test {
    useTestNG()
    workingDir = '..'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        jar(MavenPublication) {
            artifactId 'omh-schema-sdk'
            from components.java
        }
        sources(MavenPublication) {
            artifactId 'omh-schema-sdk'
            artifact sourcesJar
        }
        javadoc(MavenPublication) {
            artifactId 'omh-schema-sdk'
            artifact javadocJar
        }
    }
}
